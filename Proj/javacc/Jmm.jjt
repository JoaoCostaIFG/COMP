options
{
    LOOKAHEAD = 1;
    NODE_SCOPE_HOOK = true;
    //NODE_DEFAULT_VOID = true; // don't generate nodes by default

    //CHOICE_AMBIGUITY_CHECK=5;
    //OTHER_AMBIGUITY_CHECK=3;
    //DEBUG_PARSER=true;
    //DEBUG_LOOKAHEAD=true;
    //DEBUG_TOKEN_MANAGER=true;

    //FORCE_LA_CHECK=true;
}

PARSER_BEGIN(Jmm)

import java.util.Arrays;
import java.util.List;
import java.util.ArrayList;
import pt.up.fe.comp.jmm.report.*;
public class Jmm
{
    private List<Report> reports = new ArrayList<Report>();

    public List<Report> getReports() {
        return this.reports;
    }

    public static void main(String[] args) throws ParseException {
      	System.out.println("Write a jmm arithmetic:");
		Jmm jmm = new Jmm(System.in);
		SimpleNode root = jmm.Program(); // returns reference to root node

		root.dump(""); // prints the tree on the screen
    }

    // hooks
    private void jjtreeOpenNodeScope(Node n) {
        SimpleNode sn = (SimpleNode) n;
        Token t = getToken(1);
        sn.put("line", Integer.toString(t.beginLine));
        sn.put("col", Integer.toString(t.beginColumn));
    }

    private void jjtreeCloseNodeScope(Node n) {
    }
}

PARSER_END(Jmm)

SKIP :
{
	" " | "\r" | "\t" | "\n"
}
// comments (source: https://javacc.github.io/javacc/documentation/bnf.html)
<DEFAULT> MORE : { "//" : IN_SINGLE_LINE_COMMENT
                 | <"/**" ~["/"]> : IN_FORMAL_COMMENT
                 | "/*" : IN_MULTI_LINE_COMMENT
                 | "/*@bgen(jjtree" : IN_MULTI_LINE_COMMENT
}

<IN_SINGLE_LINE_COMMENT> SPECIAL_TOKEN : {
  <SINGLE_LINE_COMMENT: "\n" | "\r" | "\r\n"> : DEFAULT
}

<IN_FORMAL_COMMENT> SPECIAL_TOKEN : {
  <FORMAL_COMMENT: "*/"> : DEFAULT
}

<IN_MULTI_LINE_COMMENT> SPECIAL_TOKEN : {
  <MULTI_LINE_COMMENT: "*/"> : DEFAULT
}

<IN_SINGLE_LINE_COMMENT,IN_FORMAL_COMMENT,IN_MULTI_LINE_COMMENT> MORE : {
  <~[]>
}

// reserved words and literals
TOKEN:
{
    <NEW: "new">
    |   <THIS: "this">
    |   <TRUE: "true"> | <FALSE: "false">
    |   <IF: "if"> | <ELSE: "else">
    |   <WHILE: "while">
    |   <BOOL: "boolean"> | <INT: "int">
    |   <VOID: "void">
    |   <PUBLIC: "public"> | <STATIC: "static">
    |   <MAIN: "main"> | <STRING: "String">
    |   <CLASS: "class"> | <EXTENDS: "extends">
    |   <IMPORT: "import">
    |   <RET: "return">
    |   <LEN: "length"> // IMP storing as .length would enable variables to be called "length" (see DotTer())
    // |   <LF>: "\n" // we skip "\n" instead of matching it
}

// integer literals and identifiers
// IMP: unicode literal matching is not ok
TOKEN : {
    <IDENTIFIER: <LETTER> (<LETTERNUM>)*>
    |   <#LETTER: ["A"-"Z", "a"-"z", "_", "$"]>
    |   <#LETTERNUM: ["A"-"Z", "a"-"z", "0"-"9", "_", "$"]>
    |   <INTEGER_LITERAL: <DECIMAL_LITERAL> (["l","L"])?
        |   <HEX_LITERAL> (["l","L"])?
        |   <OCTAL_LITERAL> (["l","L"])?
        |   <BINARY_LITERAL> (["l","L"])?>
    | <#DECIMAL_LITERAL: ["1"-"9"] (("_")* ["0"-"9"])*>
    | <#HEX_LITERAL: "0" ["x","X"] ["0"-"9","a"-"f","A"-"F"] (("_")* ["0"-"9","a"-"f","A"-"F"])*>
    | <#OCTAL_LITERAL: "0" (("_")* ["0"-"7"])*>
    | <#BINARY_LITERAL: "0" ["b","B"] ["0","1"] (("_")* ["0","1"])*>
}

// separators
TOKEN : {
    <SPAR: "("> | <EPAR: ")">
    |   <SBRAK: "["> | <EBRAK: "]" >
    |   <SCURL: "{"> | <ECURL: "}">
    |   <DOT: "."> | <COMMA: ",">
    |   <SEMICOLON: ";">
}

// operators
TOKEN : {
    <ASSIGN: "=">
    |   <NOT: "!"> | <AND: "&&"> | <LESSTHAN: "<">
    |   <ADD: "+"> | <SUB: "-"> | <MUL: "*"> | <DIV: "/">
}

// parsing
SimpleNode Program() : {}
{
    (ImportDeclaration())* #ImportDeclarations(>0)
    ClassDeclaration() <EOF>  { return jjtThis; }
}

void ImportDeclaration() : { StringBuilder importPath = new StringBuilder(); Token packageName; }
{
    <IMPORT> packageName=<IDENTIFIER> {
        importPath=new StringBuilder(packageName.image);
        jjtThis.put("line", Integer.toString(packageName.beginLine));
    }
        (<DOT> packageName=<IDENTIFIER>{ importPath.append("." + packageName.image); })* <SEMICOLON>
        { jjtThis.put("importPath", importPath.toString()); }
}

void ClassDeclaration() : { Token className, extendsName; }
{
    // class can only extend one other class
    <CLASS> className=<IDENTIFIER> {
        jjtThis.put("className", className.image);
        jjtThis.put("line", Integer.toString(className.beginLine));
    }
    [<EXTENDS> extendsName=<IDENTIFIER> { jjtThis.put("extendsName", extendsName.image); }]
    <SCURL>
        ClassBody()
    <ECURL>
}

void ClassBody() #void : {}
{
    (VarDeclaration())* #ClassFields(>0)
    (MethodDeclaration())*
}

void VarDeclaration() : { Token varName; }
{
    Type() varName=<IDENTIFIER> {
        jjtThis.put("varName", varName.image);
        jjtThis.put("line", Integer.toString(varName.beginLine));
    } <SEMICOLON>
}

void MethodDeclaration() : { Token methodName; Token t; }
{
    <PUBLIC>
    (<STATIC> <VOID> methodName=<MAIN>
    <SPAR>
        <STRING> <SBRAK> <EBRAK> t=<IDENTIFIER>
            {
                jjtThis.put("paramName", t.image);
            } #MainParameter
    <EPAR>
    <SCURL>
        MethodBody()
    <ECURL>
    |
    Type() methodName=<IDENTIFIER>
    <SPAR>
        // the parameters node isn't "optional" (#MethodParameters(>0)) because it is easier
        // to parse the semantics if it always exists.
        [MethodParameter()  (<COMMA> MethodParameter())*] #MethodParameters
    <EPAR>
    <SCURL>
        MethodBody()
        (<RET> Expression() <SEMICOLON>) #Return
    <ECURL>)
    {
        jjtThis.put("methodName", methodName.image);
    }
}

void MethodParameter() : { Token paramName; }
{
    Type() paramName=<IDENTIFIER> {
        jjtThis.put("paramName", paramName.image);
    }
}

void MethodBody() : {}
{
    // The lines bellow make use of a LOOKAHEAD to keep the grammar correct
    (LOOKAHEAD(2) VarDeclaration())*
    (Statement())*
    // They could be changed to the lines bellow to facilitate the removal of the
    // LOOKAHEAD with the permission of the teacher.
    // The grammar now allows variables to be declared after other statements
    // have been written.
    // e.g.: i = 1; int a; // this is now valid
    //(VarDeclaration() | Statement())*
}

void Type() : { Token t; boolean isArray=false; }
{
    (t=<INT> [<SBRAK> <EBRAK> { isArray=true; }]
    |   t=<BOOL>
    |   t=<IDENTIFIER>)
    {
        jjtThis.put("dataType", t.image);
        jjtThis.put("isArray", isArray ? "yes" : "no");
    }
}

void Statement() #void : {}
{
    <SCURL> (Statement())* <ECURL> // block statment
    |   IfStatement() // if then else
    |   WhileLoop() // while loop
    |   ([LOOKAHEAD(2) Assignment()] Expression()) #Assign(>1) <SEMICOLON>
    // using semantic LOOKAHEAD instead of syntactic. This forbids contructs like: a[0];.
    // This restricts the grammar, but helps with performance (avoids infinite LOOKAHEAD).
}

void IfStatement() #If : {}
{
    <IF> <SPAR> Expression() #Cond(1) <EPAR>
        Statement() #IfBody
    <ELSE>
        Statement() #ElseBody
}

void WhileLoop() : {}
{
    <WHILE> <SPAR>

    try {
        Expression() #Cond(1)
        <EPAR>
    } catch (ParseException e) {
        error_print(e, "couldn't parse expression starting at '" +
            getToken(0).image + "' in while loop expression");
        error_skipto(new Integer[]{ EPAR, SCURL }, new Integer[]{});
        error_consume(new Integer[]{ EPAR });
    }

    Statement() #Body
}

void Assignment() #Var : { Token varName; boolean isArrayAccess = false; }
{
    varName=<IDENTIFIER> [<SBRAK> Expression() <EBRAK> { isArrayAccess = true; }] <ASSIGN>
    {
        jjtThis.put("name", varName.image);
        jjtThis.put("isArrayAccess", isArrayAccess ? "yes" : "no");
    }
}

void Expression() #void : {}
{
    LessThanTerm()
    (<AND> LessThanTerm() {
        jjtThis.put("op", "AND");
    } #Binary(2))*

    // this (and the other similar constructs) could also be written as (recursive form):
    // LessThanTerm()
    // [<AND> Expression()]
}

void LessThanTerm() #void : {}
{
    AdditiveTerm()
    (<LESSTHAN> AdditiveTerm() {
        jjtThis.put("op", "LESSTHAN");
    } #Binary(2))*
}

void AdditiveTerm() #void : {}
{
    MultiplicativeTerm()
    (<ADD> MultiplicativeTerm() {
        jjtThis.put("op", "ADD");
    } #Binary(2)
    |   <SUB> MultiplicativeTerm() {
        jjtThis.put("op", "SUB");
    } #Binary(2))*
}

void MultiplicativeTerm() #void : {}
{
    UnaryTerm()
    (<MUL> UnaryTerm() {
        jjtThis.put("op", "MULT");
    } #Binary(2)
    |   <DIV> UnaryTerm() {
        jjtThis.put("op", "DIV");
    } #Binary(2))*
}

void UnaryTerm() #void : {}
{
    <NOT> UnaryTerm() {
        jjtThis.put("op", "NOT");
    } #Unary(1)
    |   PostfixTerm()
}

void PostfixTerm() #void : {}
{
    PrimaryTerm()
    (<SBRAK> Expression() {
        jjtThis.put("op", "INDEX");
    } #Binary(2) <EBRAK>
    |   DotTerm() {
        jjtThis.put("op", "DOT");
    } #Binary(2))*
}

void DotTerm() #void : {}
{
    // If we didn't want "length" to be a reserved word, we could store <LEN> as ".length"
    // and match like so:
    // <LEN> | <DOT> DotExpansion()

    <DOT> (<LEN> #Len | DotExpansion())
}

void DotExpansion() #FuncCall : { Token t; }
{
    t=<IDENTIFIER> {
        jjtThis.put("methodName", t.image);
    }
    <SPAR>
        [(Expression() (<COMMA> Expression())*) #Args]
    <EPAR>
}

void PrimaryTerm() #void : { Token t; }
{
    <TRUE> {
        jjtThis.put("type", "boolean"); jjtThis.put("value", "true");
    } #Literal
    |   <FALSE> {
        jjtThis.put("type", "boolean"); jjtThis.put("value", "false");
    } #Literal
    |   <THIS> {
        jjtThis.put("type", "this");
    } #Literal
    |   <SPAR> Expression() <EPAR>
    |   <NEW> NewTerm()
    |   t=<INTEGER_LITERAL> {
        jjtThis.put("type", "int"); jjtThis.put("value", t.image);
    } #Literal
    |   t=<IDENTIFIER> {
        jjtThis.put("type", "identifier"); jjtThis.put("name", t.image);
    } #Literal
}

void NewTerm() #void : { Token t; }  // 04:14
{
    <INT> <SBRAK> Expression() <EBRAK> {
        jjtThis.put("type", "array");
    } #New(1)  // array allocation
    |   t=<IDENTIFIER> <SPAR> <EPAR> {
        jjtThis.put("type", "class"); jjtThis.put("name", t.image);
    } #New  // class constructor
}

// This will print the error on the terminal and save the report
JAVACODE
void error_print(ParseException e, String errMsg) {
    e.setErrMsg(errMsg);
    System.err.println(e.getErrMsg());  // print the error message
    this.reports.add(e.getReport());  // save the report
}

JAVACODE
void error_skipto(Integer[] skipto, Integer[] toconsume) {
    Token t = getToken(1);
    // consume tokens until the next token is of "kind"
    while (!Arrays.asList(skipto).contains(t.kind) && t.kind != 0) {
        getNextToken();
        t = getToken(1);
    }

    if (Arrays.asList(toconsume).contains(t.kind)) getNextToken();
}

JAVACODE
void error_consume(Integer[] toConsume) {
    // consume tokens until the next token until we find one that shouldn't be consumed
    while (Arrays.asList(toConsume).contains(getToken(1).kind)) {
        getNextToken();
    }
}